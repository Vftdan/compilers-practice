grammar SimpleAntlr;
fragment EOL_CHARS : ('\r\n'|'\r'|'\n');
fragment LBRACK : '[';
fragment RBRACK : ']';
fragment LBRACE : '{';
fragment RBRACE : '}';
fragment COMMA : ',';
fragment CARET : '^';
ACTION : '->' ~';'*;
fragment INT : [0-9]+;
fragment SIMPLE_QUANTIFIER: [*+?];
fragment QUOTE : '\'';
fragment ESQSEQ : '\\' .;
fragment STRING_CHAR : STRING_SIMPLE_CHAR;
EXPR_STRING : QUOTE (STRING_CHAR|ESQSEQ)* QUOTE;
fragment STRING_SIMPLE_CHAR : ~('\''|'\\');
fragment EXPLICIT_QUANTIFIER : LBRACE INT? COMMA INT? RBRACE;
fragment SET_TERM_CHAR : ~('\\'|']');
fragment SET_CHAR : SET_TERM_CHAR | ESQSEQ;
fragment INLINE_COMMENT_START : '//';
INLINE_COMMENT : (INLINE_COMMENT_START ~('\r'|'\n')* EOL_CHARS) -> skip;
BLOCK_COMMENT_START : '/*';
BLOCK_COMMENT_END : '*/';
BLOCK_COMMENT : (BLOCK_COMMENT_START .*? BLOCK_COMMENT_END) -> skip;
DDOT : '..';
DOT : '.';
PIPE : '|';
TILDE : '~';
LPAR : '(';
RPAR : ')';
QUANTIFIER : SIMPLE_QUANTIFIER | EXPLICIT_QUANTIFIER;
COLON : ':';
SC : ';';
GRAMMAR_KW : 'grammar ';
FRAGMENT_KW : 'fragment ';
ID : ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'0'..'9'|'_')*;
WS : (' '|'\t'|'\r'|'\n')+ -> skip;
EXPR_SET : LBRACK SET_CHAR+ RBRACK;
exprAtom : ID | EXPR_STRING | EXPR_SET | DOT | LPAR expr RPAR;
exprRange: exprAtom | exprAtom DDOT exprAtom;
exprInv: TILDE exprInv | exprRange;
exprQuantified : exprQuantified QUANTIFIER | exprInv;
exprCat : exprCat exprQuantified | exprQuantified;
exprUnion : exprUnion PIPE exprCat | exprCat;
expr : exprUnion;
exprAct : expr ACTION?;
grammarLine : GRAMMAR_KW ID SC;
exprLine : FRAGMENT_KW? ID COLON exprAct SC;
line : grammarLine | exprLine;
lang : line* EOF;
